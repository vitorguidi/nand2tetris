// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.

    //// Zero/negate x as needed
    Mux16(a=x,b[0]=false, sel=zx, out=maybezeroedx);
    Not16(in=maybezeroedx, out=maybenegatedx);
    Mux16(a=maybezeroedx, b=maybenegatedx, sel=nx, out=processedx);

    //// Zero/negate y as needed
    Mux16(a=y,b[0]=false, sel=zy, out=maybezeroedy);
    Not16(in=maybezeroedy, out=maybenegatedy);
    Mux16(a=maybezeroedy, b=maybenegatedy, sel=ny, out=processedy);

    //// Do plus or and
    And16(a=processedx, b=processedy, out=andxy);
    Add16(a=processedx, b=processedy, out=plusxy);
    Mux16(a=andxy, b=plusxy, sel=f, out=fxy);

    //// Maybe negate output
    Not16(in=fxy, out=maybenegatefxy);
    Mux16(a=fxy, b=maybenegatefxy, sel=no, out=preout);
    Or16(a=preout, b[0]=false, out=out);


    //// Check negative, trivial for 2 complement: most significant bit is 0
    Or16(a=preout, b[0..15]=false, out[15]=ng, out[0..7]=pre07, out[8..15]=pre815);


    //// Check if number is 0, reuse or8way
    Or8Way(in=pre07, out=firsthalfofout);
    Or8Way(in=pre815, out=secondhalfofout);
    Or(a=firsthalfofout, b=secondhalfofout, out=out016);
    Not(in=out016, out=zr);
}